set echo off
set messages off


# ----------- GENERATE ARTIFICIAL DATA -------------------

set seed 1234

scalar N = 1024
scalar T = 6
scalar NT = N*T
nulldata NT --preserve
setobs T 1:1 --stacked-time-series
genr time
series id = $unit

series x1 = normal()
series x2 = x1 + normal()
series x3 = randgen(t,2)

list X = x1 x2 x3

gamma = 0.6
matrix beta = ones(nelem(X),1)*0.5
s2a = 1

Ti = seq(1,N)' ~ ones(N,1)*T

A = mnormal(N,1)*s2a

ndx = lincomb(X,beta)

ini = 1
matrix Y = zeros(sumc(Ti[,2]),1)
loop i=1..N --quiet
    T = Ti[i,2] 
    
    if T>0
        fin = ini + T - 1
	ndy = ndx[ini] + A[i]
	p = exp(ndy)/(1+exp(ndy))
        Y[ini] = (randgen1(u,0,1)<p)
	
        loop t=2..T --quiet
	    ndy = ndx[ini+t-1] + gamma*Y[ini+t-2] + A[i]
	    p = exp(ndy)/(1+exp(ndy))
	    Y[ini+t-1] = (randgen1(u,0,1)<p)
        endloop
        ini = fin + 1
    endif
endloop

    
series y = Y


store QE_artdata.gdtb 



