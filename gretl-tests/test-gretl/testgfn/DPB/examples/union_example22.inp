# Replication of Wooldridge, J. M. (2005) 
# "A simple solution to the initial condition problem in dynamic, 
# nonlinear panel data models with unobserved heterogeneity"
# Journal of Applied Econometrics, 20:39-54

set echo off
set messages off

include DPB.gfn

open http://gretl.cvs.sourceforge.net/viewvc/gretl/gretl/share/data/misc/union_wooldridge.gdt

list TIME = dummify(year)
TIME -= Dyear_2
list MARR = marr8*

list X = const union_1 union80 married MARR TIME

probit union  X --random-effects --quadpoints=12 -q


m = nelem(MARR)
k   = nelem(X)
bw  = $coeff[1:k]
s2a = exp($coeff[k+1])




# --------Table II, p. 52------------------


# p11_m = P(u87=1 | u86=1, married=1) 
series ndx = bw[1] + bw[2] + bw[3]*union80 + bw[4] + lincomb(MARR,bw[5:m+4]) + bw[m+10]
series ndx /= sqrt(1+s2a)
wp11_m = cnorm(ndx)

# p10_m = P(u87=1 | u86=0, married=1) 
series ndx -= bw[2]/sqrt(1+s2a)
wp10_m =  cnorm(ndx) 

# p11 = P(u87=1 | u86=1, married=0) 
series ndx = bw[1] + bw[2] + bw[3]*union80 + lincomb(MARR,bw[5:m+4]) + bw[m+10]
series ndx /= sqrt(1+s2a)
wp11 = cnorm(ndx)

# p10 = P(u87=1 | u86=0, married=0) 
series ndx -= bw[2]/sqrt(1+s2a)
wp10 =  cnorm(ndx) 

summary wp11_m wp10_m wp11 wp10 --simple


#---- DP MODEL: Heckman (1981b) ----------

list X = const married MARR TIME
list Z = const married 

b = DPB_setup("DP",union,X,Z)
DPB_setoption(&b, "nrep", 12)
DPB_setoption(&b, "verbose", 0)
DPB_estimate(&b)
DPB_printout(&b)


k = b.nk
z = b.nz
bz = b.coeff[k+2:k+z+1]
bh = b.coeff[1:k+1]
sig_a = b.coeff[k+z+3]
s2a = sig_a^2
theta = b.coeff[k+z+2]



# hp11_m = P(u87=1 | u86=1, married87=1)
series ndx = bh[1] + bh[2] + bh[3] + lincomb(MARR, bh[4:m+3]) + bh[m+9] 
series ndx /= sqrt(1+s2a) 
series hp11_m = cnorm(ndx)

# hp10_m = P(u87=1 | u86=0, married87=1)
series ndx -= bh[1]/sqrt(1+s2a) 
series hp10_m = cnorm(ndx) 

# hp11 = P(u87=1 | u86=1, married87=0)
series ndx = bh[1] + bh[2] + lincomb(MARR, bh[4:m+3]) + bh[m+9] 
series ndx /= sqrt(1+s2a) 
series hp11 = cnorm(ndx) 

# hp10 = P(u87=1 | u86=0, married87=0)
series ndx -= bh[1]/sqrt(1+s2a) 
series hp10 = cnorm(ndx) 

summary hp11_m hp10_m hp11 hp10 --simple


