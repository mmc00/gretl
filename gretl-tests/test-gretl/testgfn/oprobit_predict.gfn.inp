include ./oprobit_predict.gfn
/* 
  Replicate the example in Wooldridge, Econometric
  Analysis of Cross Section and Panel Data (MIT Press,
  2002), section 15.10
  */
open pension.gdt

# demographic characteristics of participant
list DEMOG = age educ female black married
# dummies coding for income level
list INCOME = finc25 finc35 finc50 finc75 finc100 finc101
# response variable
series y = pctstck / 50
# estimate ordered probit
probit y choice DEMOG INCOME wealth89 prftshr
# save the z-hat values
series zhat = $yhat
# how many response values are there?
scalar nvals = rows(values(y))
scalar k = rows($coeff)
# save the last (nvals-1) coefficients in matrix cut
matrix cut = $coeff[k-nvals+2:]
# get the predicted responses
series pred = oprobit_pred(cut, zhat)

print y pred -o
pcorr = 100*sum(y == pred)/$T
pc0 = 100*sum(y == 0 && pred == 0)/sum(y==0)
pc1 = 100*sum(y == 1 && pred == 1)/sum(y==1)
pc2 = 100*sum(y == 2 && pred == 2)/sum(y==2)