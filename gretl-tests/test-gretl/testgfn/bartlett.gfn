<?xml version="1.0" encoding="UTF-8"?>
<gretl-functions>
<gretl-function-package name="bartlett" ID="1192240522" minver="1.8.4">
<author>Marcos Larios Santa Rosa</author>
<version>0.2</version>
<date>2009-08-21</date>
<description>Bartlett's test for Homogeneity of Variance</description>
<help>
This function implements Bartlett's Test for equality of variance across groups. It can be used to detect problems of heteroskedasticity in a series, e.g., the residuals of a regression.

The calculation is as shown by the National Institute of Standards and Technology's Engineering Statistics Handbook; for more information please visit: http://www.itl.nist.gov/div898/handbook/ .

The y series is the variable you want to test for homogeneity. The x series gives the basis for dividing the y values into groups.  The third argument gives the number of groups or bins to create.  If it is omitted or given as zero a group is created for each distinct value of x, otherwise the y values are grouped corresponding to nbins equally sized ranges for the x series.  Note that each group must contain at least two y values.

For more information about this function or if you want to give me some advice or improvement, please do send me an e-mail to mlsr.ftp@gmail.com
</help>
<gretl-function name="bartlett" type="void" private="0">
 <params count="3">
  <param name="y" type="series"/>
  <param name="x" type="series"/>
  <param name="nbins" type="int" min="0" default="0"/>
 </params>
<code>if nbins &gt; 0
  k = nbins
  scalar lo, hi
  scalar x_max = max(x)
  scalar x_min = min(x)
  scalar binsize = (x_max - x_min) / k
else
  matrix xvals = values(x)
  k = rows(xvals)
endif
if k &lt; 2
  funcerr &quot;There must be at least two groups&quot;
endif

N = $nobs
matrix s2 = zeros(k, 1)
matrix Ni = zeros(k, 1)
scalar s2p = 0
scalar sum1 = 0
scalar sum2 = 0
scalar err = 0

# get variance and N for each group
loop i=1..k --quiet
  if nbins &gt; 0
    hi = x_min + i * binsize
    lo = hi - binsize
    if (i == 1)
      smpl (x&lt;=hi &amp;&amp; x&gt;=lo) --restrict
    else
      smpl (x&lt;=hi &amp;&amp; x&gt;lo) --restrict
    endif
  else
    smpl (x = xvals[i]) --restrict
  endif
  if $nobs &lt; 2
    err = 1
    break
  endif
  s2[i] = var(y)
  Ni[i] = $nobs
  s2p += (Ni[i] - 1) * s2[i]
  sum1 += (Ni[i] - 1) * log(s2[i])
  sum2 += 1 / (Ni[i] - 1)
  smpl --full
endloop

if err
  funcerr &quot;Each group must have at least 2 observations&quot;
endif

s2p /= (N - k)
Tnum = (N - k) * log(s2p) - sum1
Tden = 1 + (1/(3*(k-1))) * (sum2 - 1/(N - k))
T = Tnum/Tden
df = k - 1
cv95 = critical(X, df, 0.05)
cv99 = critical(X, df, 0.01)
Tpval = pvalue(X, df, T)
matrix show = seq(1, k)' ~ sqrt(s2) ~ Ni
colnames(show, &quot;Group S(i) N(i)&quot;)

printf &quot;BARTLETT TEST FOR HOMOGENEITY OF VARIANCES\n&quot;
printf &quot;NULL HYPOTHESIS: VARIANCES ARE EQUAL\n&quot;
printf &quot;H0: S(1)=S(2)=...=S(k)\n&quot;
printf &quot;\n%12.5g\n&quot;, show
printf &quot;TEST RESULTS\n&quot;
printf &quot;   Degrees of Freedom \t\t= %10.6f\n&quot;, df
printf &quot;   Bartlett's Test Statistic \t=  %9.6f\n&quot;, T
printf &quot;   Critical Value 95%% \t\t=  %9.6f\n&quot;, cv95
printf &quot;   Critical Value 99%% \t\t=  %9.6f\n&quot;, cv99
printf &quot;   Chi-Square(%d) P-Value \t= %10.6f\n\n&quot;, df, Tpval
</code>
</gretl-function>
<sample-script>
include bartlett.gfn

# use the NIST illustrative dataset
open gear.gdt

# Is the variance of y homogeneous with respect to x?
bartlett(y, x)

# Now try using 4 bins for x
bartlett(y, x, 4)
</sample-script>
</gretl-function-package>
</gretl-functions>
